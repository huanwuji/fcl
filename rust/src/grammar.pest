WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

boolean = { "true" | "false" }
digits = _{ ("+" | "-")? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }
long = @{ digits }
float = @{ digits ~  "." ~ ASCII_DIGIT* }
null = { "null" }
char = {
    !("\"" | "'" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
inner_chars = @{ char* }
string = ${ ("\"" ~ PUSH(("\"")*) ~ inner_chars ~ "\"" ~ POP) | ("'" ~ PUSH(("'")*) ~ inner_chars ~ "'" ~ POP) }

value = ${ string | float | long | boolean | null }
variable = @{ (ASCII_ALPHANUMERIC | "_" | "$")+ }
func_name = @{ (!("(" | ")" | "." | ",") ~ ANY)+ }
argument = { function | value | variable }
arguments = { (argument ~ ("," ~ argument)*)? }
normal_func = { func_name ~ "(" ~ arguments ~ ")" }
currying_func = { func_name ~ ("(" ~ arguments? ~ ")"){2,} }
flow_func = { (currying_func | normal_func) ~ ("." ~ (currying_func | normal_func))+ }
function = { flow_func | currying_func | normal_func }
func_end = { ";" }
functions = _{ SOI ~ (function ~ func_end)* ~ function? ~ EOI }